FILESEXTRAPATHS:prepend := "${THISDIR}/${BPN}:"

require u-boot-common.inc
require u-boot.inc

COMPATIBLE_MACHINE = "(sparrow-hawk)"

DEPENDS += "lzop-native srecord-native"
DEPENDS += "bc-native dtc-native python3-pyelftools-native gnutls-native"

UBOOT_URL = "git://source.denx.de/u-boot/custodians/u-boot-sh.git"
BRANCH = "master"
SRCREV = "885fd621a309a2c3f8e8d41bdc8ff893221dc478"
SRC_URI[sha256sum] = "9e4706b93585e29ed2dd6cd964954a35f9baf8d218c461cf69a0b6c5d2ac9b4a"
LICENSE = "GPL-2.0-or-later"
LIC_FILES_CHKSUM = "file://Licenses/README;md5=2ca5f2c35c8cc335f0a19756634782f1"

SRC_URI = "${UBOOT_URL};branch=${BRANCH};protocol=https"

SRC_URI:append = " \
    file://0001-arm64-dts-renesas-Add-R8A779G3-SoC-support.patch \
    file://0002-arm64-dts-renesas-r8a779g3-Add-Renesas-R-Car-V4H-Spa.patch \
    file://0003-HACK-Enable-PWM-fan-at-boot.patch \
"
# HACK patch
SRC_URI:append = " \
    file://0004-HACK-Limit-ethernet-speed-to-100-Mbps.patch \
"
# Support BL31 load after U-Boot-SPL
SRC_URI:append = " \
    file://0005-HACK-Add-support-BL31-firmware-before-starting-U-boo.patch \
    file://0006-HACK-Skip-fdt_shrink_to_minimum-to-avoid-stack.patch \
    file://0007-HACK-ATF-missing-parts-support.patch \
    file://0008-WIP-Add-support-ATF-booting-with-SPL_FIT_LOAD_FULL-m.patch \
    file://0009-Disable-FIT-detail-log.patch \
"

PV = "v2025.04+git${SRCPV}"

UBOOT_SREC_SUFFIX = "srec"
UBOOT_SREC ?= "u-boot-elf.${UBOOT_SREC_SUFFIX}"
UBOOT_SREC_IMAGE ?= "u-boot-elf-${MACHINE}-${PV}-${PR}.${UBOOT_SREC_SUFFIX}"
UBOOT_SREC_SYMLINK ?= "u-boot-elf-${MACHINE}.${UBOOT_SREC_SUFFIX}"

do_compile[depends] += "arm-trusted-firmware:do_deploy"
do_compile:prepend() {
    cd ${S}
    sed -i arch/arm/dts/r8a779g0-u-boot.dtsi \
        -e "s|\".*bl31.*.bin\"|\"${DEPLOY_DIR}/images/${MACHINE}/bl31-${MACHINE}.bin\"|"
}

do_deploy:append() {
    if [ -n "${UBOOT_CONFIG}" ]
    then
        for config in ${UBOOT_MACHINE}; do
            i=$(expr $i + 1);
            for type in ${UBOOT_CONFIG}; do
                j=$(expr $j + 1);
                if [ $j -eq $i ]
                then
                    type=${type#*_}
                    install -m 644 ${B}/${config}/${UBOOT_SREC} ${DEPLOYDIR}/u-boot-elf-${type}-${PV}-${PR}.${UBOOT_SREC_SUFFIX}
                    cd ${DEPLOYDIR}
                    ln -sf u-boot-elf-${type}-${PV}-${PR}.${UBOOT_SREC_SUFFIX} u-boot-elf-${type}.${UBOOT_SREC_SUFFIX}
                fi
            done
            unset j
        done
        unset i
    else
        install -m 644 ${B}/${UBOOT_SREC} ${DEPLOYDIR}/${UBOOT_SREC_IMAGE}
        cd ${DEPLOYDIR}
        rm -f ${UBOOT_SREC} ${UBOOT_SREC_SYMLINK}
        ln -sf ${UBOOT_SREC_IMAGE} ${UBOOT_SREC_SYMLINK}
        ln -sf ${UBOOT_SREC_IMAGE} ${UBOOT_SREC}
        install -m 644 ${B}/flash.bin ${DEPLOYDIR}/
    fi
}
